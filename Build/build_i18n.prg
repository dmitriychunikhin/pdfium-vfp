* Code generation for class pdfium_i18n of ../Source/pdfium-vfp.vcx

LOCAL lcSourcePath
lcSourcePath = "..\Source"

LOCAL lcCodegenName
lcCodegenName = FORCEEXT(SYS(2014, SYS(16), lcSourcePath), 'prg')

MODIFY CLASS pdfium_i18n OF &lcSourcePath\pdfium-vfp.vcx NOWAIT METHOD Init

LOCAL laSelObj(1), lnSelObj
lnSelObj = ASELOBJ(laSelObj,1)

IF lnSelObj <> 1
    RETURN
ENDIF

LOCAL loPdfium_i18n
loPdfium_i18n = laSelObj[1]


LOCAL loLocales as Collection
loLocales = NEWOBJECT("Collection")
loLocales.Add(2052, "text_cn")
loLocales.Add(1031, "text_de")
loLocales.Add(1033, "text_en")
loLocales.Add(1034, "text_es")
loLocales.Add(1036, "text_fr")
loLocales.Add(1049, "text_ru")
loLocales.Add(1029, "text_cz")


CREATE CURSOR curTempText (id c(250), text m NOCPTRANS)

LOCAL lcLocaleCases
lcLocaleCases = ""

LOCAL i
FOR i = 1 TO loLocales.Count
    LOCAL lcMethod, lnLocaleID
    lcMethod = loLocales.GetKey(i)
    lnLocaleID = loLocales.Item(i)
    
    
    TEXT TO lcLocaleCases NOSHOW TEXTMERGE PRETEXT 1 FLAGS 1
        <<lcLocaleCases>>
        
        CASE This.LocaleID = <<lnLocaleID>>
            RETURN This.<<lcMethod>>(m.lcTxtID)    
    ENDTEXT
    

    SELECT * FROM curTempText WHERE 1=0 INTO CURSOR curTempText READWRITE
    
    LOCAL lcFileName
    lcFileName = lcSourcePath + "\i18n\" + lcMethod + ".xml"
    
    IF FILE(lcFileName)=.T.
        LOCAL loXMLAdapter as XMLAdapter
        loXMLAdapter = NEWOBJECT("XMLAdapter")
        loXMLAdapter.LoadXML(lcFileName, .T.)
        
        loXMLAdapter.RespectCursorCP = .F.
        loXMLAdapter.UTF8Encoded = .T.
        
        LOCAL loXmlTable as XMLTable
        loXmlTable = NEWOBJECT("XMLTable")
        loXmlTable.Alias = "curTempText"
        loXmlTable.XMLName = STRCONV("i18n",5)
        
        LOCAL loXmlField as XMLField
        loXmlField = NEWOBJECT("XMLField")
        loXmlField.Alias = "id"
        loXmlField.DataType = "C"
        loXmlField.MaxLength = 50
        loXmlField.XMLName = STRCONV(loXmlField.Alias,5)
        loXMLTable.Fields.Add(loXmlField, loXmlField.XMLName)

        loXmlField = NEWOBJECT("XMLField")
        loXmlField.Alias = "text"
        loXmlField.DataType = "M"
        loXmlField.Unicode = .T.
        loXmlField.XMLName = STRCONV(loXmlField.Alias,5)
        loXMLTable.Fields.Add(loXmlField, loXmlField.XMLName)
        
        loXMLAdapter.Tables.Add(loXmlTable, loXmlTable.XMLName)
        loXmlTable.ToCursor(.T., "curTempText")
    ENDIF

    LOCAL lcTextCases
    lcTextCases = ""
    
    LOCAL loDS
    SELECT curTempText
    SCAN
        SELECT curTempText
        SCATTER NAME loDS MEMO
        
        loDS.id = ALLTRIM(EVL(NVL(loDS.id,""),""))
        loDS.text = STRCONV(loDS.text, 6, lnLocaleID)
        
        LOCAL lcText
        lcText = ""
        
        LOCAL liChar
        FOR liChar = 1 TO LEN(loDS.text)
            lcText = NVL(EVL(lcText,NULL) + " + ", "") + TEXTMERGE("CHR(<<ASC(SUBSTR(loDS.text,liChar,1))>>)")
        ENDFOR
        
        IF EMPTY(loDS.id)
            LOOP
        ENDIF
        
        TEXT TO lcTextCases NOSHOW TEXTMERGE PRETEXT 1 FLAGS 1
            <<lcTextCases>>

            CASE m.tcTxtID == "<<loDS.id>>"
                RETURN <<lcText>>
                
        ENDTEXT

    ENDSCAN


    LOCAL lcMethodText

    TEXT TO lcMethodText NOSHOW TEXTMERGE PRETEXT 1 FLAGS 1
        * This code is generated by <<lcCodegenName>> from <<sys(2014,lcFileName,lcSourcePath)>> for LocaleID = <<lnLocaleID>>
        
        LPARAMETERS tcTxtID
        
        DO CASE

        <<lcTextCases>>
        OTHERWISE
            RETURN <<IIF(lcMethod != "text_en", "This.text_en(m.tcTxtID)", "m.tcTxtID")>>

        ENDCASE

        RETURN m.tcTxtID
        
    ENDTEXT

    loPdfium_i18n.WriteMethod(lcMethod, lcMethodText)

ENDFOR

USE IN curTempText


LOCAL lcMethodText
lcMethodText = ""
TEXT TO lcMethodText NOSHOW TEXTMERGE PRETEXT 1 FLAGS 1
    * This code is generated by <<lcCodegenName>>
    
    LPARAMETERS tcTxtID

    LOCAL lcTxtID
    m.lcTxtID = UPPER(ALLTRIM(EVL(NVL(m.tcTxtID,""),"")))

    IF EMPTY(m.lcTxtID)
        RETURN ""
    ENDIF


    DO CASE 

    <<lcLocaleCases>>
    
    OTHERWISE
        RETURN This.text_en(m.lcTxtID)

    ENDCASE
    
ENDTEXT

loPdfium_i18n.WriteMethod("text", lcMethodText)



RELEASE WINDOWS (WONTOP())
ACTIVATE WINDOW (WONTOP())
SYS(1500, "_MFI_SAVE", "_MFILE")
RELEASE WINDOWS (WONTOP())
